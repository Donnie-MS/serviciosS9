import Empresa.*
import serviciosProfesionales.*

describe "Comportamiento Empresa" {

    const sanMartin = new Universidad(provincia=#{"Buenos Aires"}, honorarios = 3500)
    const rosario = new Universidad(provincia=#{"Santa Fe"}, honorarios = 2800)
    const corrientes = new Universidad(provincia=#{"Corrientes"}, honorarios = 4200)
    const hurlingham = new Universidad(provincia=#{"Buenos Aires"}, honorarios = 8800)

    const juana = new ProfesionalVinculado(universidad = rosario)
    const melina = new ProfesionalAsociado(universidad = corrientes)
    const rocio = new ProfesionalLibre(universidad = hurlingham, honorarios= 5000, puedeTrabajar = #{"Santa Fe", "Cordoba", "Buenos Aires"})
    const luciana = new ProfesionalLibre(universidad = rosario, honorarios = 3200, puedeTrabajar = #{"Santa Fe", "Entre rios"})
    
    const empresa = new EmpresaDeServicios(honorarios= 3500)

    const pepito = new Persona(provincia= #{"Santa Fe"})

    method initialize() {
        empresa.contratar(juana)
        empresa.contratar(melina)
        empresa.contratar(rocio)
        empresa.contratar(luciana)
    }
    test "La empresa tiene cuatro profesionales y los honorarios de referencia son 3500 pesos" {
        assert.equals(4, empresa.cantProfesionalesContratados())
        assert.equals(3500, empresa.honorarios())
    }
    test "Tiene dos profesionales que estudiaron en Rosario, uno en Hurlingham, ninguno en San Martin" {
        assert.equals(2, empresa.cantProfesionalesContratados(rosario))
        assert.equals(1, empresa.cantProfesionalesContratados(hurlingham))
        assert.equals(0, empresa.cantProfesionalesContratados(sanMartin))
    }
    test "Tiene solo un profesional caro (Rocio)" {
        assert.equals(1, empresa.cantProfesionalesCaros())
        assert.that(empresa.esCaro(rocio))
    }

    test "Tiene como profesional m√°s barato a Juana" {
        assert.equals(empresa.profesionalMasBarato(), juana)
    }
    test "Tiene como universidades formadoras, a las de Rosario, Corrientes y Hurlignham" {
        assert.equals(#{rosario, corrientes, hurlingham}, empresa.universidadesFormadoras())
    }
    test "es degente acotada" {
        assert.that(empresa.esDeGenteAcotada())
    }

    test "si a luciana le agregamos corrientes y misiones, ya no es degente acotada" {
        luciana.agregarProvincia("Corrientes")
        luciana.agregarProvincia("Misiones")
        assert.notThat(empresa.esDeGenteAcotada())
    }
    test "los profesionales libres se guardan todo lo que cobran" {
        rocio.cobrar(2000)
        assert.equals(2000, rocio.dinero())
    }
    test "Un profesional libre le puede pasar una cantidad X de dinero a otro" {
        rocio.cobrar(1000)
        rocio.pasarDinero(luciana, 500)
        assert.equals(500, luciana.dinero())
    }
    test "Profesional vinculado (juana) puede trabajar solamente en Santa Fe" {
        assert.equals(#{"Santa Fe"}, juana.puedeTrabajar())
    }
    test "Profesional asociado (melina) puede trabajar en Entre Rios, Santa Fe, Corrientes" {
        assert.equals(#{"Entre Rios", "Santa Fe", "Corrientes"}, melina.puedeTrabajar())
    }
    test "Profesional libre (luciana) puede trabajar en Santa fe, Entre rios" {
        assert.equals(#{"Santa Fe", "Entre rios"}, luciana.puedeTrabajar())
    }
    test "pepito puede ser atendido por juana" {
        assert.that(pepito.puedeSerAtendido(juana))
    }
    test "Se puede dar servicio una persona(pepito)" {
        empresa.darServicio(pepito)
        assert.equals(0, juana.dinero())
        assert.equals(1400, juana.universidad().donaciones())
        assert.that(empresa.esCliente(pepito))
    }
}
